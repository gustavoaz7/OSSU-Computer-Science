// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Mux16 (a=x, b=false, sel=zx, out=xP1);
    Not16(in=xP1, out=nxP1);
    Mux16(a=xP1, b=nxP1, sel=nx, out=xP2);

    Mux16 (a=y, b=false, sel=zy, out=yP1);
    Not16(in=yP1, out=nyP1);
    Mux16(a=yP1, b=nyP1, sel=ny, out=yP2);

    Add16(a=xP2, b=yP2, out=xPlusy);
    And16(a=xP2, b=yP2, out=xAndy);
    Mux16(a=xAndy, b=xPlusy, sel=f, out=fout);

    Not16(in=fout, out=nfout);
    Mux16(a=fout, b=nfout, sel=no, out=outTemp, out[0..7]=outTemp1, out[8..15]=outTemp2);

    And16(a=true, b=outTemp, out=out, out[15]=ng);

    Or8Way(in=outTemp1, out=outTempOr1);
    Or8Way(in=outTemp2, out=outTempOr2);
    Or(a=outTempOr1, b=outTempOr2, out=nzr);
    Not(in=nzr, out=zr);
}