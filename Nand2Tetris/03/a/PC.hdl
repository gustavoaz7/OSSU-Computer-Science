// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Or8Way(in[0]=reset, in[1]=load, in[2]=inc, in[3..7]=false, out=loadPC);
    Add16(a=prevOut, b[0]=true, b[1..15]=false, out=incPrevOut);

    Mux16(a=prevOut, b=incPrevOut, sel=inc, out=temp1);
    Mux16(a=temp1, b=in, sel=load, out=temp2);
    Mux16(a=temp2, b=false, sel=reset, out=temp3);

    Register(in=temp3, load=loadPC, out=out, out=prevOut);
}
